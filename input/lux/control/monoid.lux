##   Copyright (c) Eduardo Julian. All rights reserved.
##   The use and distribution terms for this software are covered by the
##   Eclipse Public License 1.0 (http://opensource.org/licenses/eclipse-1.0.php)
##   which can be found in the file epl-v10.html at the root of this distribution.
##   By using this software in any fashion, you are agreeing to be bound by
##   the terms of this license.
##   You must not remove this notice, or any other, from this software.

(;import lux
         (lux/data ord
                   (bounded #as B)))

## Signatures
(defsig #export (Monoid a)
  (: a
     unit)
  (: (-> a a a)
     ++))

## Constructors
(def #export (monoid$ unit ++)
  (All [a]
    (-> a (-> a a a) (Monoid a)))
  (struct
    (def unit unit)
    (def ++ ++)))

## Structures
(defstruct #export Maybe:Monoid (Monoid Maybe)
  (def unit #;None)
  (def (++ xs ys)
    (case xs
      #;None     ys
      (#;Some x) (#;Some x))))

(defstruct #export List:Monoid (All [a]
                                 (Monoid (List a)))
  (def unit #;Nil)
  (def (++ xs ys)
    (case xs
      #;Nil            ys
      (#;Cons [x xs']) (#;Cons [x (++ xs' ys)]))))

(do-template [<name> <type> <unit> <++>]
  [(defstruct #export <name> (Monoid <type>)
     (def unit <unit>)
     (def ++ <++>))]

  [ IntAdd:Monoid  Int 0    int:+]
  [ IntMul:Monoid  Int 1    int:*]
  [RealAdd:Monoid Real 0.0 real:+]
  [RealMul:Monoid Real 1.0 real:*]
  [ IntMax:Monoid   Int (::  B;Int:Bounded B;bottom)  (max  Int:Ord)]
  [ IntMin:Monoid   Int (::  B;Int:Bounded B;top)     (min  Int:Ord)]
  [RealMax:Monoid  Real (:: B;Real:Bounded B;bottom)  (max Real:Ord)]
  [RealMin:Monoid  Real (:: B;Real:Bounded B;top)     (min Real:Ord)]
  )
