##   Copyright (c) Eduardo Julian. All rights reserved.
##   The use and distribution terms for this software are covered by the
##   Eclipse Public License 1.0 (http://opensource.org/licenses/eclipse-1.0.php)
##   which can be found in the file epl-v10.html at the root of this distribution.
##   By using this software in any fashion, you are agreeing to be bound by
##   the terms of this license.
##   You must not remove this notice, or any other, from this software.

(;import lux
         (lux/control (monoid #as m))
         (.. (eq #as E)
             (ord #as O)
             (bounded #as B)
             (show #as S)))

## Signatures
(defsig #export (Number n)
  (do-template [<name>]
    [(: (-> n n n) <name>)]
    [+] [-] [*] [/] [%])

  (: (-> Int n)
     from-int)

  (do-template [<name>]
    [(: (-> n n) <name>)]
    [negate] [signum] [abs])
  )

## [Structures]
## Number
(do-template [<name> <type> <+> <-> <*> </> <%> <=> <<> <from> <0> <1> <-1>]
  [(defstruct #export <name> (Number <type>)
     (def + <+>)
     (def - <->)
     (def * <*>)
     (def / </>)
     (def % <%>)
     (def (from-int x)
       (<from> x))
     (def (negate x)
       (<*> <-1> x))
     (def (abs x)
       (if (<<> x <0>)
         (<*> <-1> x)
         x))
     (def (signum x)
       (cond (<=> x <0>) <0>
             (<<> x <0>) <-1>
             ## else
             <1>))
     )]

  [ Int/Number  Int i+ i- i* i/ i% i= i<       id 0   1   -1]
  [Real/Number Real r+ r- r* r/ r% r= r< _jvm_l2d 0.0 1.0 -1.0])

## Eq
(defstruct #export Int/Eq (E;Eq Int)
  (def E;= i=))

(defstruct #export Real/Eq (E;Eq Real)
  (def E;= r=))

## Ord
## (def #export Int/Ord (O;Ord Int)
##   (O;ord$ Int/Eq  i< i>))

## (def #export Real/Ord (O;Ord Real)
##   (O;ord$ Real/Eq r< r>))

(do-template [<name> <type> <eq> <lt> <gt>]
  [(defstruct #export <name> (O;Ord <type>)
     (def O;_eq <eq>)
     (def O;< <lt>)
     (def (O;<= x y)
       (or (<lt> x y)
           (using <eq> (E;= x y))))
     (def O;> <gt>)
     (def (O;>= x y)
       (or (<gt> x y)
           (using <eq> (E;= x y)))))]

  [ Int/Ord  Int  Int/Eq i< i>]
  [Real/Ord Real Real/Eq r< r>])

## Bounded
(do-template [<name> <type> <top> <bottom>]
  [(defstruct #export <name> (B;Bounded <type>)
     (def B;top <top>)
     (def B;bottom <bottom>))]

  [ Int/Bounded  Int (_jvm_getstatic java.lang.Long   MAX_VALUE) (_jvm_getstatic java.lang.Long   MIN_VALUE)]
  [Real/Bounded Real (_jvm_getstatic java.lang.Double MAX_VALUE) (_jvm_getstatic java.lang.Double MIN_VALUE)])

## Monoid
(do-template [<name> <type> <unit> <++>]
  [(defstruct #export <name> (m;Monoid <type>)
     (def m;unit <unit>)
     (def m;++ <++>))]

  [ IntAdd/Monoid  Int 0   i+]
  [ IntMul/Monoid  Int 1   i*]
  [RealAdd/Monoid Real 0.0 r+]
  [RealMul/Monoid Real 1.0 r*]
  [ IntMax/Monoid  Int (::  Int/Bounded B;bottom)  (O;max  Int/Ord)]
  [ IntMin/Monoid  Int (::  Int/Bounded B;top)     (O;min  Int/Ord)]
  [RealMax/Monoid Real (:: Real/Bounded B;bottom)  (O;max Real/Ord)]
  [RealMin/Monoid Real (:: Real/Bounded B;top)     (O;min Real/Ord)]
  )

## Show
(do-template [<name> <type> <body>]
  [(defstruct #export <name> (S;Show <type>)
     (def (S;show x)
       <body>))]

  [ Int/Show  Int (_jvm_invokevirtual java.lang.Object toString [] x [])]
  [Real/Show Real (_jvm_invokevirtual java.lang.Object toString [] x [])]
  )
