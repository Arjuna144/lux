##   Copyright (c) Eduardo Julian. All rights reserved.
##   The use and distribution terms for this software are covered by the
##   Eclipse Public License 1.0 (http://opensource.org/licenses/eclipse-1.0.php)
##   which can be found in the file epl-v10.html at the root of this distribution.
##   By using this software in any fashion, you are agreeing to be bound by
##   the terms of this license.
##   You must not remove this notice, or any other, from this software.

(;import lux
         (lux/control (number #as N)
                      (monoid #as m)
                      (eq #as E)
                      (ord #as O)
                      (bounded #as B)
                      (show #as S)))

## [Structures]
## Number
(do-template [<name> <type> <+> <-> <*> </> <%> <=> <<> <from> <0> <1> <-1>]
  [(defstruct #export <name> (N;Number <type>)
     (def (+ x y) (<+> x y))
     (def (- x y) (<-> x y))
     (def (* x y) (<*> x y))
     (def (/ x y) (</> x y))
     (def (% x y) (<%> x y))
     (def (from-int x)
       (<from> x))
     (def (negate x)
       (<*> <-1> x))
     (def (abs x)
       (if (<<> x <0>)
         (<*> <-1> x)
         x))
     (def (signum x)
       (cond (<=> x <0>) <0>
             (<<> x <0>) <-1>
             ## else
             <1>))
     )]

  [ Int/Number  Int _jvm_ladd _jvm_lsub _jvm_lmul _jvm_ldiv _jvm_lrem _jvm_leq _jvm_llt id 0   1   -1])

## Eq
(defstruct #export Int/Eq (E;Eq Int)
  (def (= x y) (_jvm_leq x y)))

## Ord
(do-template [<name> <type> <eq> <=> <lt> <gt>]
  [(defstruct #export <name> (O;Ord <type>)
     (def _eq <eq>)
     (def (< x y) (<lt> x y))
     (def (<= x y)
       (or (<lt> x y)
           (<=> x y)))
     (def (> x y) (<gt> x y))
     (def (>= x y)
       (or (<gt> x y)
           (<=> x y))))]

  [ Int/Ord  Int  Int/Eq _jvm_leq _jvm_llt _jvm_lgt])

## Bounded
(do-template [<name> <type> <top> <bottom>]
  [(defstruct #export <name> (B;Bounded <type>)
     (def top <top>)
     (def bottom <bottom>))]

  [ Int/Bounded  Int (_jvm_getstatic "java.lang.Long"   "MAX_VALUE") (_jvm_getstatic "java.lang.Long"   "MIN_VALUE")])

## Monoid
(do-template [<name> <type> <unit> <++>]
  [(defstruct #export <name> (m;Monoid <type>)
     (def unit <unit>)
     (def (++ x y) (<++> x y)))]

  [ IntAdd/Monoid  Int 0 _jvm_ladd]
  [ IntMul/Monoid  Int 1 _jvm_lmul]
  [ IntMax/Monoid  Int (::  Int/Bounded B;bottom)  (O;max  Int/Ord)]
  [ IntMin/Monoid  Int (::  Int/Bounded B;top)     (O;min  Int/Ord)]
  )

## Show
(do-template [<name> <type> <body>]
  [(defstruct #export <name> (S;Show <type>)
     (def (show x)
       <body>))]

  [ Int/Show  Int (_jvm_invokevirtual "java.lang.Object" "toString" [] x [])]
  )
