##  Copyright (c) Eduardo Julian. All rights reserved.
##  This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
##  If a copy of the MPL was not distributed with this file,
##  You can obtain one at http://mozilla.org/MPL/2.0/.

(;import lux
         (lux/control (number #as N)
                      (monoid #as m)
                      (eq #as E)
                      (ord #as O)
                      (bounded #as B)
                      (show #as S)))

## [Structures]
## Number
(do-template [<name> <type> <+> <-> <*> </> <%> <=> <<> <from> <0> <1> <-1>]
  [(defstruct #export <name> (N;Number <type>)
     (def (N;+ x y) (<+> x y))
     (def (N;- x y) (<-> x y))
     (def (N;* x y) (<*> x y))
     (def (N;/ x y) (</> x y))
     (def (N;% x y) (<%> x y))
     (def (N;from-int x)
       (<from> x))
     (def (N;negate x)
       (<*> <-1> x))
     (def (N;abs x)
       (if (<<> x <0>)
         (<*> <-1> x)
         x))
     (def (N;signum x)
       (cond (<=> x <0>) <0>
             (<<> x <0>) <-1>
             ## else
             <1>))
     )]

  [Real/Number Real _jvm_dadd _jvm_dsub _jvm_dmul _jvm_ddiv _jvm_drem _jvm_deq _jvm_dlt _jvm_l2d 0.0 1.0 -1.0])

## Eq
(defstruct #export Real/Eq (E;Eq Real)
  (def (E;= x y) (_jvm_deq x y)))

## Ord
(do-template [<name> <type> <eq> <=> <lt> <gt>]
  [(defstruct #export <name> (O;Ord <type>)
     (def O;_eq <eq>)
     (def (O;< x y) (<lt> x y))
     (def (O;<= x y)
       (or (<lt> x y)
           (<=> x y)))
     (def (O;> x y) (<gt> x y))
     (def (O;>= x y)
       (or (<gt> x y)
           (<=> x y))))]

  [Real/Ord Real Real/Eq _jvm_deq _jvm_dlt _jvm_dgt])

## Bounded
(do-template [<name> <type> <top> <bottom>]
  [(defstruct #export <name> (B;Bounded <type>)
     (def B;top <top>)
     (def B;bottom <bottom>))]

  [Real/Bounded Real (_jvm_getstatic "java.lang.Double" "MAX_VALUE") (_jvm_getstatic "java.lang.Double" "MIN_VALUE")])

## Monoid
(do-template [<name> <type> <unit> <++>]
  [(defstruct #export <name> (m;Monoid <type>)
     (def m;unit <unit>)
     (def (m;++ x y) (<++> x y)))]

  [RealAdd/Monoid Real 0.0 _jvm_dadd]
  [RealMul/Monoid Real 1.0 _jvm_dmul]
  [RealMax/Monoid Real (:: Real/Bounded B;bottom)  (O;max Real/Ord)]
  [RealMin/Monoid Real (:: Real/Bounded B;top)     (O;min Real/Ord)]
  )

## Show
(do-template [<name> <type> <body>]
  [(defstruct #export <name> (S;Show <type>)
     (def (S;show x)
       <body>))]

  [Real/Show Real (_jvm_invokevirtual "java.lang.Object" "toString" [] x [])]
  )
