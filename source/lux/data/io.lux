##   Copyright (c) Eduardo Julian. All rights reserved.
##   The use and distribution terms for this software are covered by the
##   Eclipse Public License 1.0 (http://opensource.org/licenses/eclipse-1.0.php)
##   which can be found in the file epl-v10.html at the root of this distribution.
##   By using this software in any fashion, you are agreeing to be bound by
##   the terms of this license.
##   You must not remove this notice, or any other, from this software.

(;import lux
         (lux/meta macro)
         (lux/control (functor #as F)
                      (monad #as M))
         (.. list
             (text #as T #open ("text:" Text/Monoid))))

## Types
(deftype #export (IO a)
  (-> (,) a))

## Syntax
(defmacro #export (io tokens state)
  (case tokens
    (\ (list value))
    (let [blank (symbol$ ["" ""])]
      (#;Right [state (list (` (_lux_lambda (~ blank) (~ blank) (~ value))))]))

    _
    (#;Left "Wrong syntax for io")))

## Structures
(defstruct #export IO/Functor (F;Functor IO)
  (def (F;map f ma)
    (io (f (ma [])))))

(defstruct #export IO/Monad (M;Monad IO)
  (def M;_functor IO/Functor)
  
  (def (M;wrap x)
    (io x))

  (def (M;join mma)
    (mma [])))

## Functions
(def #export (print x)
  (-> Text (IO (,)))
  (io (_jvm_invokevirtual "java.io.PrintStream" "print" ["java.lang.Object"]
                          (_jvm_getstatic "java.lang.System" "out") [x])))

(def #export (println x)
  (-> Text (IO (,)))
  (io (_jvm_invokevirtual "java.io.PrintStream" "println" ["java.lang.Object"]
                          (_jvm_getstatic "java.lang.System" "out") [x])))
