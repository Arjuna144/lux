##  Copyright (c) Eduardo Julian. All rights reserved.
##  This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
##  If a copy of the MPL was not distributed with this file,
##  You can obtain one at http://mozilla.org/MPL/2.0/.

(;import lux
         (lux/control (functor #as F #refer #all)
                      (monad #as M #refer #all)
                      (comonad #as CM #refer #all)))

## [Types]
(deftype #export (Id a)
  (| (#Id a)))

## [Structures]
(defstruct #export Id/Functor (Functor Id)
  (def (map f fa)
    (let [(#Id a) fa]
      (#Id (f a)))))

(defstruct #export Id/Monad (Monad Id)
  (def _functor Id/Functor)
  (def (wrap a) (#Id a))
  (def (join mma) (let [(#Id ma) mma] ma)))

(defstruct #export Id/CoMonad (CoMonad Id)
  (def _functor Id/Functor)
  (def (unwrap wa) (let [(#Id a) wa] a))
  (def (split wa) (#Id wa)))
